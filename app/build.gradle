plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "dev.forcecodes.truckme"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    kapt {
        javacOptions {
            // These options are normally set automatically via the Hilt Gradle plugin, but we
            // set them manually to workaround a bug in the Kotlin 1.5.20
            option("-Adagger.fastInit=ENABLED")
            option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview"
        ]
    }

    flavorDimensions "truckme"

    productFlavors {
        admin {
            dimension "truckme"
            applicationIdSuffix ".admin"
            versionNameSuffix "-admin"
            resValue "string", "app_name", "Truck Me Admin"
        }

        driver {
            dimension "truckme"
            applicationIdSuffix ".driver"
            versionNameSuffix "-driver"
            resValue "string", "app_name", "Truck Me Driver"
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation project(path: ':core')
    implementation project(path: ':libs')
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    def nav_version = "2.4.0-alpha10"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'

    implementation platform('com.google.firebase:firebase-bom:28.4.0')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha03'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0-alpha03"

    implementation "com.google.dagger:hilt-android:2.38.1"
    kapt "com.google.dagger:hilt-compiler:2.38.1"

    implementation 'com.nambimobile.widgets:expandable-fab:1.1.1'

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation "com.zhihu.android:matisse:0.5.3-beta3"

    implementation "androidx.recyclerview:recyclerview:1.3.0-alpha01"

    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.maps.android:maps-ktx:3.1.0'
    implementation "com.google.android.gms:play-services-location:18.0.0"
    implementation 'com.google.android.libraries.places:places:2.4.0'
    implementation 'com.google.maps.android:android-maps-utils:2.2.6'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.4.1'

    // suppress
    // Method getAlpnSelectedProtocol not supported for object SSL socket over Socket
    implementation "io.grpc:grpc-okhttp:1.32.2"

    implementation "com.github.skydoves:powerspinner:1.1.9"
}